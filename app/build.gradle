apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.gladed.androidgitversion'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion build_versions.compile_sdk
    buildToolsVersion build_versions.build_tools

    androidGitVersion {
        prefix 'v'
    }

    def properties = file("../gradle.properties")
    def hasProperties = properties.exists()

    defaultConfig {
        applicationId 'de.uni_marburg.mathematik.ds.serval'
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()

        setProperty('archivesBaseName', "aardvark-$versionName")
        multiDexEnabled true

        if (hasProperties) {
            manifestPlaceholders = [KEY_FABRIC: KEY_FABRIC, KEY_GOOGLE_MAPS: KEY_GOOGLE_MAPS]
        } else {
            throw new InvalidUserDataException("Define Fabric and Google Maps keys")
        }
    }

    signingConfigs {
        if (hasProperties) {
            release {
                storeFile = file(STORE_FILE)
                storePassword = STORE_PASSWORD
                keyAlias = KEY_ALIAS
                keyPassword = KEY_PASSWORD
            }
        } else {
            throw new InvalidUserDataException("Define release signing secrets")
        }
    }

    buildTypes {
        buildTypes.each {
            it.buildConfigField "String", "KERVAL_VERSION", "\"$versions.kerval\""
        }

        debug {
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled false
            if (hasProperties) signingConfig signingConfigs.release
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {
    // App Toolkit
    implementation files("libs/kerval-${versions.kerval}.jar")
    implementation(deps.crashlytics) { transitive = true }
    implementation deps.google_maps_clustering
    implementation(deps.moshi) { exclude group: 'com.squareup.okio', module: 'okio' }
    implementation deps.reprint
    implementation(deps.smartlocation) { transitive = false }
    // Architecture Components
    implementation deps.lifecycle.extensions
    implementation deps.room.runtime
    implementation deps.paging
    kapt deps.lifecycle.compiler
    kapt deps.room.compiler
    // Firebase
    implementation deps.firebase.core
    implementation deps.firebase.perf
    // Google Play Services
    implementation deps.google_play_services.location
    implementation deps.google_play_services.maps
    // Ioconics
    implementation deps.ioconics.community_material_typeface
    implementation deps.ioconics.weather_icons_typeface
    // Kau
    implementation deps.kau.kau_about
    implementation deps.kau.kpref_activity
    // Kotlin
    implementation deps.kotlin.stdlib
    // LeakCanary
    debugImplementation deps.leak_canary
    releaseImplementation deps.leak_canary_release
}

apply plugin: 'com.google.gms.google-services'
