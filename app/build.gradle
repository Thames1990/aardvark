plugins {
    id 'com.gladed.androidgitversion' version '0.4.3'
}

apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion build_versions.compile_sdk
    buildToolsVersion build_versions.build_tools

    androidGitVersion {
        prefix 'v'
    }

    defaultConfig {
        applicationId 'de.uni_marburg.mathematik.ds.serval'
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()

        setProperty('archivesBaseName', "aardvark-$versionName")
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        try {
            manifestPlaceholders = [KEY_FABRIC: KEY_FABRIC, KEY_GOOGLE_MAPS: KEY_GOOGLE_MAPS]
        } catch (ignored) {
            throw new InvalidUserDataException(
                    "You should define Fabric and Google Maps keys in gradle.properties."
            )
        }
    }

    signingConfigs {
        release {
            try {
                storeFile = file(STORE_FILE)
                storePassword = STORE_PASSWORD
                keyAlias = KEY_ALIAS
                keyPassword = KEY_PASSWORD
            } catch (ignored) {
                throw new InvalidUserDataException(
                        "You should define release signing secrets in gradle.properties."
                )
            }
        }
    }

    buildTypes {
        buildTypes.each {
            it.buildConfigField "String", "KERVAL_VERSION", "\"$versions.kerval\""
        }

        debug {
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }
}

dependencies {
    // App Toolkit
    implementation files("libs/kerval-${versions.kerval}.jar")
    implementation deps.ah_bottom_navigation
    implementation(deps.crashlytics) { transitive = true}
    implementation deps.google_maps_clustering
    implementation deps.ktx
    implementation deps.reprint
    // Architecture Components
    implementation deps.lifecycle.extensions
    implementation deps.room.runtime
    implementation deps.paging
    kapt deps.lifecycle.compiler
    kapt deps.room.compiler
    // Firebase
    implementation deps.firebase.core
    implementation deps.firebase.perf
    // Google Play Services
    implementation deps.google_play_services.location
    implementation deps.google_play_services.maps
    // Ioconics
    implementation deps.ioconics.community_material_typeface
    implementation deps.ioconics.weather_icons_typeface
    // Kau
    implementation deps.kau.kau_about
    implementation deps.kau.kpref_activity
    // Kotlin
    implementation deps.kotlin.stdlib
    implementation deps.kotlin.reflect
    // LeakCanary
    debugImplementation deps.leak_canary
    releaseImplementation deps.leak_canary_release
    // Moshi
    implementation deps.moshi.java
    implementation deps.moshi.kotlin
}

apply plugin: 'com.google.gms.google-services'
