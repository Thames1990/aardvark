apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'
    
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    
    defaultConfig {
        applicationId 'de.uni_marburg.mathematik.ds.serval'
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 23
        versionName '0.4.12-20'
        setProperty('archivesBaseName', "aardvark-$versionName")
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
        manifestPlaceholders = [
                KEY_FABRIC     : keystoreProperties['key_fabric'],
                KEY_GOOGLE_MAPS: keystoreProperties['key_google_maps']
        ]
    }
    
    signingConfigs {
        release {
            storeFile = file(keystoreProperties['storeFile'])
            storePassword = keystoreProperties['storePassword']
            keyAlias = keystoreProperties['keyAlias']
            keyPassword = keystoreProperties['keyPassword']
        }
    }
    
    buildTypes {
        debug {
            ext.enableCrashlytics = false
            manifestPlaceholders = [
                    APP_ICON      : '@mipmap/ic_launcher_dev',
                    APP_ICON_ROUND: '@mipmap/ic_launcher_round_dev'
            ]
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [
                    APP_ICON      : '@mipmap/ic_launcher',
                    APP_ICON_ROUND: '@mipmap/ic_launcher_round'
            ]
        }
    }
    
    androidExtensions {
        experimental = true
    }
    
    dataBinding {
        enabled = true
    }
}

dependencies {
    
    compile fileTree(include: ['*.jar'], dir: 'libs')
    
    // Android Maps Utils
    compile "com.google.maps.android:android-maps-utils:$android_maps_utils_version"
    
    // Architecture Components
    compile "android.arch.lifecycle:extensions:$architecture_components_version"
    annotationProcessor "android.arch.lifecycle:compiler:$architecture_components_version"
    compile "android.arch.persistence.room:runtime:$architecture_components_version"
    annotationProcessor "android.arch.persistence.room:compiler:$architecture_components_version"
    compile "android.arch.paging:runtime:$paging_version"
    
    // Crashlytics
    compile "com.crashlytics.sdk.android:crashlytics:$crashlytics_version"
    
    // Firebase
    compile "com.google.firebase:firebase-core:$google_play_services_version"
    
    // Google Play Services
    compile "com.google.android.gms:play-services-location:$google_play_services_version"
    compile "com.google.android.gms:play-services-maps:$google_play_services_version"
    
    // KAU
    compile "ca.allanwang.kau:core:$kau_version"
    
    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    
    // LeakCanary
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leak_canary_version"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
    
    // Markwon
    compile "ru.noties:markwon:$markwon_version"
    
    // MaterialIntroScreen
    compile "agency.tango.android:material-intro-screen:$material_intro_screen_version"
    
    // Moshi
    compile "com.squareup.moshi:moshi:$moshi_version"
    compile "com.squareup.moshi:moshi-kotlin:$moshi_version"
    
    // OkHttp
    compile "com.squareup.okhttp3:okhttp:$okhttp_version"
    
    // Support libraries
    compile "com.android.support:appcompat-v7:$support_library_version"
    compile "com.android.support:cardview-v7:$support_library_version"
    compile "com.android.support.constraint:constraint-layout:$constraint_layout_version"
    compile "com.android.support:design:$support_library_version"
    compile "com.android.support:preference-v7:$support_library_version"
    compile "com.android.support:preference-v14:$support_library_version"
    compile "com.android.support:recyclerview-v7:$support_library_version"
    compile "com.android.support:support-v4:$support_library_version"
    compile "com.android.support:support-vector-drawable:$support_library_version"
}

apply plugin: 'com.google.gms.google-services'
