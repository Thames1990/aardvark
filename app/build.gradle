plugins {
    id 'com.gladed.androidgitversion' version '0.4.3'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    androidGitVersion {
        prefix 'v'
    }

    defaultConfig {
        applicationId 'de.uni_marburg.mathematik.ds.serval'
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        setProperty('archivesBaseName', "aardvark-$versionName")
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        try {
            manifestPlaceholders = [KEY_FABRIC: KEY_FABRIC, KEY_GOOGLE_MAPS: KEY_GOOGLE_MAPS]
        } catch (ignored) {
            throw new InvalidUserDataException(
                    "You should define Fabric and Google Maps keys in gradle.properties."
            )
        }
    }

    signingConfigs {
        release {
            try {
                storeFile = file(STORE_FILE)
                storePassword = STORE_PASSWORD
                keyAlias = KEY_ALIAS
                keyPassword = KEY_PASSWORD
            } catch (ignored) {
                throw new InvalidUserDataException(
                        "You should define release signing secrets in gradle.properties."
                )
            }
        }
    }

    buildTypes {
        debug {
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    // Test
    androidTestImplementation(deps.espresso.core, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Support Libraries
    implementation deps.support.app_compat
    implementation deps.support.design
    implementation deps.support.recyclerview

    // Architecture Components
    implementation deps.lifecycle.runtime
    implementation deps.lifecycle.extensions
    implementation deps.room.runtime
    implementation deps.paging
    kapt deps.lifecycle.compiler
    kapt deps.room.compiler

    // App Toolkit
    implementation deps.kotlin.stdlib
    implementation deps.crashlytics
    implementation deps.firebase
    implementation deps.google_maps_clustering
    implementation deps.google_play_services_location
    implementation deps.google_play_services_maps
    implementation deps.community_material_typeface
    implementation deps.weather_icons_typeface
    implementation deps.kau_about
    implementation deps.kpref_activity
    implementation files("libs/kerval-0.0.8-1.jar")
    implementation deps.okhttp
    implementation deps.reprint

    // Moshi
    implementation deps.moshi
    implementation deps.moshi_kotlin

    // LeakCanary
    debugImplementation deps.leak_canary
    releaseImplementation deps.leak_canary_release

    // Kotlin
    implementation deps.kotlin.stdlib
    implementation deps.kotlin.reflect

    // Android Testing Support Library's runner and rules
    androidTestImplementation deps.atsl.runner
    androidTestImplementation deps.atsl.rules
    androidTestImplementation deps.room.testing
    androidTestImplementation deps.arch_core.testing
}

apply plugin: 'com.google.gms.google-services'
