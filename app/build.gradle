apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven {url 'https://maven.fabric.io/public'}
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'
    
    defaultConfig {
        applicationId 'de.uni_marburg.mathematik.ds.serval'
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 11
        versionName '0.4.0'
        setProperty('archivesBaseName', "$applicationId-$versionName")
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
        
        jackOptions {
            enabled true
        }
    }
    
    applicationVariants.all {variant ->
        variant.resValue("string", "versionName", variant.versionName)
    }
    
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    
    signingConfigs {
        release {
            storeFile = file(keystoreProperties['storeFile'])
            storePassword = keystoreProperties['storePassword']
            keyAlias = keystoreProperties['keyAlias']
            keyPassword = keystoreProperties['keyPassword']
        }
    }
    
    buildTypes {
        debug {
            manifestPlaceholders = [
                    KEY_FABRIC     : keystoreProperties['key_fabric'],
                    KEY_GOOGLE_MAPS: keystoreProperties['key_google_maps']
            ]
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [
                    KEY_FABRIC     : keystoreProperties['key_fabric'],
                    KEY_GOOGLE_MAPS: keystoreProperties['key_google_maps']
            ]
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    
    ext {
        butterknife_version = '8.8.1'
        google_play_services_version = '11.2.2'
        leak_canary_version = '1.5.2'
        material_dialogs_version = '0.9.4.7'
        support_library_version = '26.0.1'
    }
    
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile(
            'com.android.support.test.espresso:espresso-core:2.2.2',
            {exclude group: 'com.android.support', module: 'support-annotations'}
    )
    
    // Butter Knife
    compile "com.jakewharton:butterknife:${butterknife_version}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterknife_version}"
    
    // Fabric
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
    
    // Firebase
    compile "com.google.firebase:firebase-core:${google_play_services_version}"
    
    // Google Play Services
    compile "com.google.android.gms:play-services-location:${google_play_services_version}"
    compile "com.google.android.gms:play-services-maps:${google_play_services_version}"
    
    // LeakCanary
    debugCompile "com.squareup.leakcanary:leakcanary-android:${leak_canary_version}"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leak_canary_version}"
    
    // Material Dialogs
    compile "com.afollestad.material-dialogs:core:${material_dialogs_version}"
    compile "com.afollestad.material-dialogs:commons:${material_dialogs_version}"
    
    // MarkdownView
    compile 'us.feras.mdv:markdownview:1.1.0'
    
    // Moshi
    compile 'com.squareup.moshi:moshi:1.5.0'
    
    // OkHttp
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    
    // Support libraries
    compile "com.android.support:appcompat-v7:${support_library_version}"
    compile "com.android.support:cardview-v7:${support_library_version}"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile "com.android.support:design:${support_library_version}"
    compile "com.android.support:support-v4:${support_library_version}"
    compile "com.android.support:support-vector-drawable:${support_library_version}"
    compile "com.android.support:recyclerview-v7:${support_library_version}"
    
    // Test libraries
    testCompile 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'
